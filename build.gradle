import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
	id 'org.springframework.boot' version '3.2.5' apply false
	id 'io.spring.dependency-management' version '1.1.4'
	id 'org.jetbrains.kotlin.jvm' version '1.9.23'
	id 'org.jetbrains.kotlin.plugin.spring' version '1.9.23'
	id 'org.jetbrains.kotlin.plugin.jpa' version '1.9.23'
	id 'com.diffplug.spotless'  version "6.25.0"
	id 'org.jetbrains.kotlinx.kover' version "0.8.0"
}

group = 'com.example.otel'
version = '0.0.1-SNAPSHOT'

dependencies {
	kover(project(":app"))
	kover(project(":application"))
	kover(project(":domain"))
	kover(project(":infrastructure"))
}

kover {
	reports {
		verify {
			rule {
				minBound(70)
			}
		}
	}
}

allprojects {
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'org.jetbrains.kotlin.jvm'
	apply plugin: 'org.jetbrains.kotlin.plugin.spring'
	apply plugin: 'org.jetbrains.kotlin.plugin.jpa'
	apply plugin: 'com.diffplug.spotless'
	apply plugin: 'org.jetbrains.kotlinx.kover'

	java {
		sourceCompatibility = '21'
	}

	repositories {
		mavenCentral()
	}

	dependencyManagement {
		imports {
			mavenBom("org.springframework.boot:spring-boot-dependencies:3.2.5")
		}
		dependencies {
			dependency 'io.kotest:kotest-runner-junit5:5.9.0'
			dependency 'io.kotest.extensions:kotest-extensions-spring:1.3.0'
		}
	}

	tasks.withType(KotlinCompile) {
		kotlinOptions {
			freeCompilerArgs += '-Xjsr305=strict'
			jvmTarget = '21'
		}
	}

	tasks.named('test') {
		useJUnitPlatform()
	}
}

spotless {
	format 'misc', {
		target '**/*.gradle', '.gitattributes', '.gitignore'

		trimTrailingWhitespace()
		indentWithTabs()
		endWithNewline()
	}
	kotlin {
		target '**/*.kt'
		ktfmt("0.49")
	}
}
